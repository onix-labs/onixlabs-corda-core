buildscript {
    ext {
        kotlin_group = 'org.jetbrains.kotlin'
        kotlin_version = '1.2.71'

        corda_artifactory_url = 'https://software.r3.com/artifactory'
        corda_group = 'net.corda'
        corda_release_version = '4.6'

        corda_gradle_plugin_group = 'net.corda.plugins'
        corda_gradle_plugin_version = '5.0.4'

        junit_group = 'org.junit.jupiter'
        junit_version = '5.3.1'

        cordapp_platform_version = 8
        cordapp_signing_enabled = true
        cordapp_contract_name = 'ONIXLabs Corda Core Contract'
        cordapp_workflow_name = 'ONIXLabs Corda Core Workflow'
        cordapp_vendor_name = 'ONIXLabs'
        cordapp_license = 'Apache License, Version 2.0'
        cordapp_version_id = 1
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "$corda_artifactory_url/corda-releases" }
        maven { url "$corda_artifactory_url/corda-dependencies" }
    }

    dependencies {
        classpath "$kotlin_group:kotlin-gradle-plugin:$kotlin_version"
        classpath "$corda_gradle_plugin_group:cordapp:$corda_gradle_plugin_version"
    }
}

group 'io.onixlabs'
version '1.0.0-rc4'

subprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "https://jitpack.io" }
        maven { url "$corda_artifactory_url/corda-releases" }
        maven { url "$corda_artifactory_url/corda-dependencies" }
        maven { url "https://repo.gradle.org/gradle/libs-releases" }
    }

    configurations {
        all {
            exclude module: 'slf4j-log4j12'
            exclude module: 'log4j-slf4j-impl'
        }
    }

    apply plugin: 'kotlin'
    apply plugin: 'maven-publish'
    apply plugin: 'net.corda.plugins.cordapp'

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            languageVersion = "1.2"
            apiVersion = "1.2"
            jvmTarget = "1.8"
            javaParameters = true
        }
    }

    jar { exclude '**/log4j2*.xml' }

    test {
        jvmArgs = ["-ea", "-javaagent:../lib/quasar.jar"]
        maxHeapSize = "4096m"
        useJUnitPlatform()
    }
}

task cleanLocal(type: Exec) {
    def shellExec = System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows') ? 'cmd' : 'sh'
    commandLine shellExec, './clean-cache.sh'
}

task releaseLocal(type: GradleBuild) {
    startParameter = gradle.startParameter.newInstance()
    tasks = ['cleanLocal', 'clean', 'build', 'publishToMavenLocal']
}

task releasePublic(type: GradleBuild) {
    startParameter = gradle.startParameter.newInstance()
    tasks = ['cleanLocal', 'clean', 'build', 'publishToMavenLocal', 'publish']
}
